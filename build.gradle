import org.ajoberstar.grgit.Grgit
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.10"
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "org.ajoberstar.grgit" version "1.7.2"
}

apply plugin: 'kotlin'

//Kotlin options
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        suppressWarnings = true
    }
}

static def timestamp() {
    return new Date().format("yyyy-MM-dd HH:mm:ssZ", TimeZone.getDefault())
}

//Get git commit id
ext {
    git = Grgit.open(file('.'))
    revision = git.head().getAbbreviatedId(7)
}

//Force resources processing
processResources.outputs.upToDateWhen { false }

//Token replacing with plugin info for 'plugin.yml'
processResources {
    filter ReplaceTokens, tokens: [
            'pl.name'   : project.name,
            'pl.group'  : project.group,
            'pl.version': project.version + ' (Built at ' + timestamp() + ' from revision ' + revision + ')',
            'pl.authors': 'MrPingu_'
    ]
}

//Jar shadowing with no classifier nor version on file name
shadowJar {
    classifier = null
    version = null
}

//Copy shadowed jar to 'plugins' folder of local server
task copyJar(type: Copy) {
    from shadowJar
    into 'I:\\Programming\\Spigot Servers\\Dusty\\plugins'
}

repositories {
    jcenter()
    mavenLocal()

    maven { url "https://repo.viaversion.com/" }
    maven { url "http://repo.dmulloy2.net/content/groups/public/" }
    maven { url "http://maven.sk89q.com/repo/" }
}

dependencies {
    compileOnly project(":DustyCommon")

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.2.10"

    compileOnly "org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT"

    compileOnly "us.myles:viaversion:1.3.0"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.4.0-SNAPSHOT"
    compileOnly 'com.sk89q.worldguard:worldguard-legacy:6.2'

    compileOnly "io.reactivex.rxjava2:rxjava:2.1.7"
    compileOnly "io.reactivex.rxjava2:rxkotlin:2.2.0"
    compileOnly "joda-time:joda-time:2.9.9"
    compileOnly "org.apache.httpcomponents:httpclient:4.5.3"
    //compile "ch.qos.logback:logback-classic:1.2.3"
    //compile "org.codehaus.groovy:groovy:2.4.12"
}
