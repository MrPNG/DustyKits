import org.ajoberstar.grgit.Grgit
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.60"
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "org.ajoberstar.grgit" version "1.7.2"
}

apply plugin: 'kotlin'

//Kotlin options
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        suppressWarnings = true
    }
}

static def timestamp() {
    Date date = new Date()

    return date.format("yyyy-MM-dd HH:mm:ssZ", TimeZone.getDefault())
}

//Get git commit id
ext {
    git = Grgit.open(file('.'))
    revision = git.head().getAbbreviatedId(7)
}

//Force resources processing
processResources.outputs.upToDateWhen { false }

//Token replacing with plugin info for 'plugin.yml'
processResources {
    filter ReplaceTokens, tokens: [
            'pl.name'   : project.name,
            'pl.group'  : project.group,
            'pl.version': project.version + ' (Built at ' + timestamp() + ' from revision ' + revision + ')',
            'pl.authors': 'MrPingu_'
    ]
}

//Jar shadowing with no classifier nor version on file name
shadowJar {
    classifier = null
    version = null
}

//Copy shadowed jar to plugins folder on 'run' folder
task copyJar(type: Copy) {
    from shadowJar
    into '/run/plugins/'
}

//Run Spigot server from Gradle command line (but it isn't closed when build is completed)
task runServer(type: Exec) {
    workingDir '/run/'
    standardInput = System.in

    commandLine 'cmd', '/c java -Xmx256M -jar spigot.jar'
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    compileOnly "org.spigotmc:spigot:$spigotVersion"

    compile "joda-time:joda-time:2.9.9"
    compile "org.apache.httpcomponents:httpclient:4.5.3"
    //compile "ch.qos.logback:logback-classic:1.2.3"
    //compile "org.codehaus.groovy:groovy:2.4.12"
}
