import org.ajoberstar.grgit.Grgit
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.4"

    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "org.ajoberstar.grgit" version "1.7.2"
}

apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'

//Plugin Info
group 'br.com.dusty.dkits'
version '1.0.0'

//Java Version
sourceCompatibility = 1.8
targetCompatibility = 1.8

//Build timestamp String
static def timestamp() {
    return new Date().format("yyyy-MM-dd'T'HH:mm:ssZ", TimeZone.getDefault())
}

//Get git commit id
ext {
    git = Grgit.open(file('.'))
    revision = git.head().getAbbreviatedId(7)
}

//Always process resources
processResources.outputs.upToDateWhen { false }

//Token replacing with plugin info on 'plugin.yml'
processResources {
    filter ReplaceTokens, tokens: [
            'pl.name'   : project.name,
            'pl.group'  : project.group,
            'pl.version': project.version + ' (Built at ' + timestamp() + ' from revision ' + revision + ')',
            'pl.authors': 'MrPingu_'
    ]
}

//Kotlin options
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

//Jar shadowing with no classifier nor version
shadowJar {
    classifier = null
    version = null
}

//Copy jar to plugins folder on 'run' folder
task copyJar(type: Copy) {
    from shadowJar
    into '/run/plugins/'
}

//Run Spigot server from Gradle command line
task runServer(type: Exec) {
    workingDir '/run/'
    standardInput = System.in

    commandLine 'cmd', '/c java -Xmx256M -jar spigot.jar'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.4"

    compileOnly group: 'org.spigotmc', name: 'spigot', version: '1.12-R0.1-SNAPSHOT'

    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'

    //compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    //compile group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.0-beta-1'
}
